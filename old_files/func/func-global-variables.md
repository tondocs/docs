[https://github.com/ton-blockchain/docs/blob/master/docs/func/global_variables.md]

Глобальные переменные

Программа FunC - это, по сути, список объявлений/определений функций и объявлений глобальных переменных. В этом разделе рассматривается вторая тема.

Глобальная переменная может быть объявлена с помощью ключевого слова global, за которым следует тип переменной и ее имя. Например,

global ((int, int) -> int) op;

int check_assoc(int a, int b, int c) {
  return op(op(a, b), c) == op(a, op(b, c));
}

int main() {
  op = _+_;
  return check_assoc(2, 3, 9);
}


это простая программа, которая записывает в глобальную функциональную переменную op оператор сложения _+_ и проверяет ассоциативность сложения на трех образцах целых чисел 2, 3 и 9.

Внутренние глобальные переменные хранятся в регистре управления c7 TVM.

Тип глобальной переменной может быть не указан. В этом случае он будет определяться по использованию переменной. Например, мы можем переписать программу как:

global op;

int check_assoc(int a, int b, int c) {
  return op(op(a, b), c) == op(a, op(b, c));
}

int main() {
  op = _+_;
  return check_assoc(2, 3, 9);
}


Можно объявить несколько переменных после одного и того же global. Следующие коды эквивалентны:

global int A;
global cell B;
global C;


global int A, cell B, C;


Не допускается объявлять какую-либо локальную переменную с тем же именем, что и уже объявленная глобальная переменная. Например, этот код не будет компилироваться:

global cell C;

int main() {
  int C = 3;
  return C;
}


Обратите внимание, что этот код корректен:

global int C;

int main() {
  int C = 3;
  return C;
}


но здесь int C = 3; эквивалентно C = 3;, т.е. это присвоение глобальной переменной C, а не объявление локальной переменной C (объяснение этого эффекта можно найти в выражениях).